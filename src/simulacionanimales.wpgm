import wollok.game.*
import animales.*
import comedero.*

program animales {
	// configuracion de pantalla
	game.width(10)
	game.height(10)

	/*
	 * agregamos personajes
	 */ 
	var comedero1 = new Comedero(pesoMaximo = 150)
	var comedero2 = new Comedero(position = game.at(2,2), pesoMaximo = 2)
	var gallina = new Gallina()
	var vaca = new Vaca()
	var animalActual = gallina
	game.addVisual(animalActual)
	game.addVisual(comedero1)
	game.addVisual(comedero2)
	/*
	 * configuraciones de teclado
	 */
	keyboard.c().onPressDo({ => animalActual.comer(12) })
	keyboard.p().onPressDo({
		game.say(animalActual, animalActual.peso().toString())
	})
	keyboard.s().onPressDo({
		if(animalActual.tieneSed()){
			game.say(animalActual, "Tengo sed.")
		}else{
			game.say(animalActual, "No tengo sed.")
		}
	})
	keyboard.b().onPressDo({ animalActual.beber() })
	
	//moverse
	
	keyboard.up().onPressDo {
		animalActual.subir()
	}
	keyboard.down().onPressDo {
		animalActual.bajar()
	}
	keyboard.left().onPressDo {
		animalActual.izquierda()
	}
	keyboard.right().onPressDo {
		animalActual.derecha()
	}
	
	//transformacion
	
	keyboard.t().onPressDo{
		game.removeVisual(animalActual)
		if(animalActual == gallina ){
			animalActual = vaca
		}else{
			animalActual = gallina
		}
		game.addVisual(animalActual)
	}
	
	//colisiones
	
	game.whenCollideDo(comedero1, { animal =>
		animal.bajar()
		if( comedero1.puedeAtender(animal) ) {
			if(comedero1.cantRaciones() == 0){
				game.say(comedero1,"No tenemos mas raciones de comida.")
			}
			else{
				comedero1.atender(animal)
				game.say(animal, "Gracias por la comida.")
			}
		}else{
			game.say(comedero1,"No te puedo atender.")
		}
	})
	
	game.whenCollideDo(comedero2, { animal =>
		animal.bajar()
		if( comedero2.puedeAtender(animal) ) {
			if(comedero2.cantRaciones() == 0){
				game.say(comedero2,"No tenemos mas raciones de comida.")
			}
			else{
				comedero2.atender(animal)
				game.say(animal, "Gracias por la comida.")
			}
		}else{
			game.say(comedero2,"No te puedo atender.")
		}
	})
	
	
	/*
	 * arrancamos el juego
	 */
	 game.start()
}